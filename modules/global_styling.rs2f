/*@ define:module:global_defaults
---
name: Global Defaults
subtitle: The first layer of styling and filtering
description: |
  This provides some default styling options to apply to all items, unless overridden by a module below. This also provides an option to explicitly show and hide specific items. This list however can be overridden in a below modules as well.
*/


/*@ define:input:global_defaults
type: boolean
label: Sort menu by item value
*/
#define VAR_GLOBAL_MENU_SORT_BY_VALUE true

/*@ define:input:global_defaults
type: stringlist
label: Items to hide
group: Item Filters
*/
#define VAR_GLOBAL_HIDE []

apply (name:VAR_GLOBAL_HIDE) {
    hidden = true;
}

/*@ define:input:global_defaults
type: stringlist
label: Items to show
group: Item Filters
*/
#define VAR_GLOBAL_SHOW []

apply (name:VAR_GLOBAL_SHOW) {
    hidden = false;
}

/*@ define:input:global_defaults
type: number
label: Hide items below this value
group: Item Filters
*/
#define VAR_GLOBAL_MINIMUM_VALUE 0

apply ((tradeable:true || name:CONST_GLOBAL_TRADEABLE_NON_TRADEABLE_ITEMS) && value:<VAR_GLOBAL_MINIMUM_VALUE) {
    hidden = true;
}

/*@ define:input:global_defaults
type: number
label: Hide stackable items below this value
group: Item Filters
*/
#define VAR_GLOBAL_STACKABLE_MINIMUM_VALUE 0

apply ((tradeable:true || name:CONST_GLOBAL_TRADEABLE_NON_TRADEABLE_ITEMS) && (stackable:true || noted:true) && value:<VAR_GLOBAL_STACKABLE_MINIMUM_VALUE) {
    hidden = true;
}

/*@ define:input:global_defaults
type: number
label: Hide non-stackable items below this value
group: Item Filters
*/
#define VAR_GLOBAL_NON_STACKABLE_MINIMUM_VALUE 0

apply ((tradeable:true || name:CONST_GLOBAL_TRADEABLE_NON_TRADEABLE_ITEMS) && stackable:false && noted:false && value:<VAR_GLOBAL_NON_STACKABLE_MINIMUM_VALUE) {
    hidden = true;
}

/*@ define:input:global_defaults
type: boolean
label: Hide world spawn items
group: Item Filters
*/
#define VAR_GLOBAL_HIDE_SPAWN_ITEMS false

apply (ownership:OWNERSHIP_NONE && VAR_GLOBAL_HIDE_SPAWN_ITEMS) {
    hidden = true;
}

/*@ define:input:global_defaults
type: boolean
label: Hide items owned by other players
group: Item Filters
*/
#define VAR_GLOBAL_HIDE_OTHER_PLAYERS_ITEMS false

apply (ownership:OWNERSHIP_OTHER && VAR_GLOBAL_HIDE_OTHER_PLAYERS_ITEMS) {
    hidden = true;
}

/*@ define:input:global_defaults
type: style
label: Global default style
group: Style Settings
exampleItem: Santa hat
exampleItemId: 1050
*/
#define VAR_GLOBAL_CUSTOMSTYLE \
  icon = CurrentItem();\
  showDespawn = true;

apply (true) {
    VAR_GLOBAL_CUSTOMSTYLE 
}

/*@ define:input:global_defaults
type: boolean
label: Show Value for Tradeable Items
group: Style Settings
*/
#define VAR_GLOBAL_SHOW_TRADEABLE_VALUE true

apply ((tradeable:true || name:CONST_GLOBAL_TRADEABLE_NON_TRADEABLE_ITEMS) && VAR_GLOBAL_SHOW_TRADEABLE_VALUE) {
    showValue = true;
}

/*@ define:input:global_defaults
type: boolean
label: Show Value for Untradeable Items
group: Style Settings
*/
#define VAR_GLOBAL_SHOW_UNTRADEABLE_VALUE false

apply (tradeable:false && VAR_GLOBAL_SHOW_UNTRADEABLE_VALUE) {
    showValue = true;
}

/*@ define:input:global_defaults
type: number
label: Hide despawn timer for items below this value
group: Style Settings
*/
#define VAR_GLOBAL_DESPAWN_TIMER_MINIMUM_VALUE 0

apply ((tradeable:true || name:CONST_GLOBAL_TRADEABLE_NON_TRADEABLE_ITEMS) && value:<VAR_GLOBAL_DESPAWN_TIMER_MINIMUM_VALUE) {
    showDespawn = false;
}

/*@ define:input:global_defaults
type: number
label: Hide prices for items below this value
group: Style Settings
*/
#define VAR_GLOBAL_SHOW_PRICES_MINIMUM_VALUE 0

apply ((tradeable:true || name:CONST_GLOBAL_TRADEABLE_NON_TRADEABLE_ITEMS) && value:<VAR_GLOBAL_SHOW_PRICES_MINIMUM_VALUE) {
    showValue = false;
}

/*@ define:input:global_defaults
type: number
label: Hide icons for items below this value
group: Style Settings
*/
#define VAR_GLOBAL_ICON_MINIMUM_VALUE 0

apply ((tradeable:true || name:CONST_GLOBAL_TRADEABLE_NON_TRADEABLE_ITEMS) && value:<VAR_GLOBAL_ICON_MINIMUM_VALUE) {
    // this is a hack to turn off the icon
    icon = Sprite(9000000, 0);
}

/*@ define:input:global_defaults
type: stringlist
label: Items to show icons for (if not showing for all items)
group: Style Settings
*/
#define VAR_GLOBAL_SHOW_ICONS_ITEMLIST []

apply (name:VAR_GLOBAL_SHOW_ICONS_ITEMLIST) {
    icon = CurrentItem();
}
